# Maintainer:  Tim Stahlhut <stahta01@gmail.com>

_realname=cc65
_sourcedir=${_realname}-git
pkgbase=${_realname}
pkgname="${_realname}-git"
provides=("${_realname}")
conflicts=("${_realname}")
pkgver=2.19.r1435.d1bf3ba8c
pkgrel=1
pkgdesc="C compiler for 6502 family microprocessors"
arch=('i686' 'x86_64')
options=('!strip') 
license=('BSD')
makedepends=('git'
             'gcc')
url='https://cc65.github.io/cc65/'
source=(${_sourcedir}::git+https://github.com/cc65/cc65)
sha512sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_sourcedir}

  printf "%s" "$(git describe --tags --long --first-parent \
                 | sed 's/^V//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd ${srcdir}/${_sourcedir}

  git clean -fxd
}

build() {
  cd ${srcdir}/${_sourcedir}

  make PREFIX=${MSYSTEM_PREFIX}
}

package() {
  cd ${srcdir}/${_sourcedir}

  make DESTDIR="$pkgdir" PREFIX=${MSYSTEM_PREFIX} install
  install -Dm644 "LICENSE" "$pkgdir${MSYSTEM_PREFIX}/share/licenses/$_realname/license"
}
