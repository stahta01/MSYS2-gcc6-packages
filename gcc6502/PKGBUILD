# Maintainer:  Tim Stahlhut <stahta01@gmail.com>

_BUGURL="https://github.com/stahta01/MSYS2-gcc6-packages/issues"


_last_gcc_commit=457a4c2e2d7ebc5d6943d378e129a7653a8d44d3

_realname=gcc6502
_sourcedir=${_realname}-git
pkgbase=${_realname}
pkgname="${_realname}-git"
provides=("${_realname}")
conflicts=("${_realname}")
pkgver=6.0.0.r138533.c1057
pkgrel=3
pkgdesc="GCC C compiler for 6502 family microprocessors"
arch=('i686' 'x86_64')
options=('!strip')
license=('GPL2')
makedepends=('git' 'flex' 'texinfo' 'diffutils'
             'cc65' 'gcc6')
url='https://github.com/itszor/gcc-6502/'
source=(${_sourcedir}::git+https://github.com/itszor/gcc-6502
        '6502-ar'
        '6502-ranlib')
sha512sums=('SKIP'
            'eee9ebb4f46569f76c940ff1d99acd14b2235bcee2e03411c2949544aab3d203acec79b8db8661d05834a2473a3d86a6be59ab4891ba1da4d2659c78e4e6b492'
            'b16d8486464d73d9fab7192c6b4893b75554e80cda5292d2ec51db30e2a9c3bea6b137c311f14e82d17aec67b7c77570f877b181f95fca51c06504e6856861b6')

_git_prepatch_commit=

pkgver() {
  cd ${srcdir}/${_sourcedir}

  if [[ -n "${_git_prepatch_commit}" ]]; then
    printf "%s.r%s.c%s" "6.0.0" $(git rev-list --count ${_last_gcc_commit}) \
      $(git rev-list --count ${_last_gcc_commit}..${_git_prepatch_commit})
  else
    printf "%s" ${pkgver}
  fi
}

prepare() {
  cd ${srcdir}/${_sourcedir}
  _git_prepatch_commit=$(git rev-parse HEAD)

}

build() {
  # Replace the MSys2 default FLAG values
  export LDFLAGS="-pipe"
  export CPPFLAGS=""
  export CFLAGS="-pipe"

  MORE_OPTIONS="--enable-checking"
  DEBUG_FLAGS="-O0 -g"

  CA65_PATH="${MSYSTEM_PREFIX}/bin/ca65"
  LD65_PATH="${MSYSTEM_PREFIX}/bin/ld65"

  echo ""
  echo "CC [gcc] := ${CC-gcc}"
  ${CC-gcc} --version | head -1
  #pacman -Q ${MINGW_PACKAGE_PREFIX}-headers
  #pacman -Q ${MINGW_PACKAGE_PREFIX}-crt
  echo ""

  if [[ -d "build-${MSYSTEM}" ]]; then
    rm -rf build-${MSYSTEM}
  fi
  mkdir build-${MSYSTEM} && cd build-${MSYSTEM}

  pwd

  ../${_sourcedir}/configure \
      --prefix=${MSYSTEM_PREFIX} \
      --datarootdir=${MSYSTEM_PREFIX}/share/6502 \
      --with-sysroot=${MSYSTEM_PREFIX}/6502 \
      --with-build-sysroot=${MSYSTEM_PREFIX}/6502 \
      --target=6502 \
      --enable-languages=c \
      --with-as=${CA65_PATH} \
      --with-ld=${LD65_PATH} \
      --without-headers \
      --with-newlib \
      --disable-nls \
      --disable-decimal-float \
      --disable-libssp \
      --disable-threads \
      --disable-libatomic \
      --disable-libitm \
      --disable-libsanitizer \
      --disable-libquadmath \
      --disable-lto \
      --with-pkgversion="Rev${pkgrel} ${MSYSTEM}, Built by stahta01" \
      --with-bugurl=${_BUGURL} \
      --enable-sjlj-exceptions \
      --without-isl $MORE_OPTIONS

      make -j1 all-gcc AR_FOR_TARGET="${srcdir}/6502-ar" RANLIB_FOR_TARGET="${srcdir}/6502-ranlib" BOOT_CFLAGS="$DEBUG_FLAGS" CFLAGS="$DEBUG_FLAGS" CXXFLAGS="$DEBUG_FLAGS"
}

package() {
  cd ${srcdir}/build-${MSYSTEM}

  make -j1 RANLIB_FOR_TARGET="${srcdir}/6502-ranlib" DESTDIR="$pkgdir/" install-gcc
}
